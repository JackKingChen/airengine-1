//#include "Common.h"
ByteAddressBuffer		voxeloctree		:	register(t0);
StructuredBuffer<uint>	voxelColor		:	register(t1);
TextureCube				Dir2Order		:	register(t2);		
Texture2D				Depth			:	register(t3);
Texture2D				Normal			:	register(t4);
SamplerState			SS_WarpPoint	:	register(s0);	
cbuffer cbPerCamera : register( b1 )
{
    matrix  ViewProjection;
	float4	vCameraDir;
	float4	vCameraPosition;
	float4	vCameraUp;
	float4	vVP_NearFar;
   
}
cbuffer cbPerMaterial	:	register(b3)
{
	matrix	matViewProjInv;
}

//#define USE_SPHERE_CHECK

bool	RayCastBoundingBox(float3 vStart,float3 vDir,float3 vMin,float3 vMax,in float fMaxDistance,inout float fOutDistance)

{
	float3	fmin		=	vMin	-	vStart;
	float3	fmax		=	vMax	-	vStart;
	float3	invMinDir	=	fmin	/	vDir;
	float3	invMaxDir	=	fmax	/	vDir;

	fOutDistance		=	10000.0f;

	float3	fNear		=	min(invMinDir,invMaxDir);
	float3	fFar		=	max(invMinDir,invMaxDir);
	float	maxNear		=	max(max(fNear.x,fNear.y),fNear.z);
	float	minFar		=	min(min(fFar.x,fFar.y),fFar.z);
	if(	maxNear	>	minFar	||
		maxNear	>	fMaxDistance	||
		minFar	<	0.0f)
	{
		return	false;
	}
	if(maxNear	<	0.0f)
		maxNear	=	0.0f;
	fOutDistance	=	maxNear;	
	return	true;
};


float	RayCastBoundingBox2(float3 vStart,float3 vDir,float3 vMin,float3 vMax,out float2 fNearFar)

{
	
	float3	fmin		=	vMin	-	vStart;
	float3	fmax		=	vMax	-	vStart;
	float3	invMinDir	=	fmin	/	vDir;
	float3	invMaxDir	=	fmax	/	vDir;

	
	float3	fNear		=	min(invMinDir,invMaxDir);
	float3	fFar		=	max(invMinDir,invMaxDir);
	fNearFar.x		=	max(max(fNear.x,fNear.y),fNear.z);
	fNearFar.y		=	min(min(fFar.x,fFar.y),fFar.z);
	if(fNearFar.x	<	0.0f)
		fNearFar.x	=	0.0f;
	return	fNearFar.y-fNearFar.x;
};

float RayCastSphere(float3 vStart,float3 vDir,float3 vSphere,float fSize)
{
	float3	vSphere_Eye	=	vSphere   -   vStart;
	float	fCos		=	dot(normalize(vSphere_Eye),vDir);
    float   fSphereCos            =   cos(atan(fSize/length(vSphere_Eye)));
    return fCos-fSphereCos;
}

uint	CalcOrder(float3 vDir){
	float aData[8];
	uint  anIndex[8]={0,1,2,3,4,5,6,7};
	uint  uiOrder=0;
	for(uint i=0;i<8;i++){
		aData[i]=dot(float3(i>>2,1&(i>>1),1&i),vDir);	
	}
	uint N2=8;
	    // Bitonic sort
    for( uint k = 2; k <= N2; k = 2*k )
    {
        for( uint j = k>>1; j > 0 ; j = j>>1 ) 
        {
            for( uint i = 0; i < N2; i++ ) 
            {
                float di = aData[ anIndex[ i ] ];
                uint ixj = i^j;
                if ( ( ixj ) > i )
                {
                    float dixj = aData[ anIndex[ ixj ] ];
                    if ( ( i&k ) == 0 && di > dixj )
                    { 
                        int temp = anIndex[ i ];
                        anIndex[ i ] = anIndex[ ixj ];
                        anIndex[ ixj ] = temp;
                    }
                    if ( ( i&k ) != 0 && di < dixj )
                    {
                        int temp = anIndex[ i ];
                        anIndex[ i ] = anIndex[ ixj ];
                        anIndex[ ixj ] = temp;
                    }
                }
            }
        }
    }
	for(uint n=0;n<8;n++){
		uiOrder|=(anIndex[n])<<3*n;	
	}
	return uiOrder;
}

struct VS_OUTPUT
{
    float4 Position     : SV_POSITION; // vertex position
	float2 UV			: TEXCOORD0;
	float4 WorldPos		: TEXCOORD1;
};

struct	PS_OUTPUT{
	float4	Diff			:	SV_Target0;
};

float4	U32_F32(uint uiColor){
	return	float4(
					((uiColor&0x00ff0000)>>16)/255.0f,
					((uiColor&0x0000ff00)>>8)/255.0f,
					((uiColor&0x000000ff))/255.0f,
					((uiColor&0xff000000)>>24)/255.0f
			);
}
uint	F32_U32(float4 c){
	uint4 ic=c*255;
	return	ic.z|ic.y<<8|ic.x<<16|ic.w<<24;
}
uint2	PositionToIndex(float3 vCamPos,float3 vDir,float2 fNearFar,float3 vMin,float3 vHalfSize){
	float3 vTemp			=	vCamPos-vMin-vHalfSize;
	float3	fFirstInterSect	=	vDir*fNearFar.x+vTemp;
	uint	fFirstIndex		=	dot(fFirstInterSect>0,float3(4,2,1));
	float3  fEndInterSect	=	vDir*fNearFar.y+vTemp;
	uint	fEndIndex		=	dot(fEndInterSect>0,float3(4,2,1));
	return uint2(fFirstIndex,fEndIndex);
}
#define	MAX_DEPTH	9
#define VOXEL_BOUND_SIZE	pow(2,MAX_DEPTH-1);
//#define INTERSECT_NORMAL_PLANE

float4 main( VS_OUTPUT Input ):SV_Target0{
	float2 xy	=	Input.UV*float2(2,-2)+float2(-1,1);

	float z	=	Depth.Sample(SS_WarpPoint,Input.UV).x;
	if(z > 0.9999f){
		discard;
	}
	
	float4	wpos	=	mul(matViewProjInv,float4(xy,z,1));
	wpos/=wpos.w;
	float3 vCamPos	=	vCameraPosition.xyz;
	float3 dir	=	normalize(wpos.xyz	-	vCamPos	);
	float3	normal	=	Normal.Sample(SS_WarpPoint,Input.UV).xyz*2-1;
	dir		=	normalize(reflect(dir,normal));
	vCamPos			=	wpos.xyz+dir*2;

	uint uiOffset[MAX_DEPTH];
	uiOffset[0]=0;
	uint uiStack=0;
	uint uiEndStack=0;

	int	uiDepth	=	0;
	float3	vHalfSize	=	VOXEL_BOUND_SIZE;
	float3  vMin		=	-vHalfSize;
	float2	fNearFar;
	if(RayCastBoundingBox2(vCamPos,dir,vMin,vHalfSize,fNearFar)	<	0){
		discard;
		return 0;
	}
	//return ;
	uint uiOrder = F32_U32(Dir2Order.SampleLevel(SS_WarpPoint,dir,0));
	uint uiOrderToIndex=0;
	for(uint i=0;i<8;i++){
		uint uiIdx	=	(uiOrder>>(i*3))&0x7;
		uiOrderToIndex|=i<<(3*uiIdx);
	}
	
	//return U32_F32(uiOrder);
	uint	retColor	=	0;
	float3 vCenter			=	vMin+vHalfSize;

	uint2	uiHitIndex		=	PositionToIndex(vCamPos,dir,fNearFar,vMin,vHalfSize);
	int iter		=	(uiOrderToIndex>>(uiHitIndex.x*3))&0x7;
	int iterSize	=	((uiOrderToIndex>>(uiHitIndex.y*3))&0x7);

	[loop]
	while(true){
		
		[loop]
		for(;iter<=iterSize;){
			
			uint childidx	=	(uiOrder>>(iter*3))&0x7;

			uint addr		=	(uiOffset[uiDepth]+childidx)*4;
			uint uiChild	=	voxeloctree.Load(addr);

			//float fCmp=uiChild*1.001f;
			if(uiChild==0){
				iter++;
				continue;
			}

			float3	vNewMin	=	vMin+float3(childidx>>2,1&(childidx>>1),1&childidx)*vHalfSize;
			
			
			
			float3	vNewMax	=	vNewMin+vHalfSize;
			float fDis	=	RayCastBoundingBox2(vCamPos,dir,vNewMin,vNewMax,fNearFar);
			if(fDis<0.0f){
				iter++;
				continue;
			}


		
	

			if(uiDepth	==	MAX_DEPTH-1){
#ifdef INTERSECT_NORMAL_PLANE
				float4 vVoxelNormal	=	U32_F32(uiChild);
				vVoxelNormal=vVoxelNormal*2-1;
				vVoxelNormal.w*=0.866025f;
				
				float3  p	=	vCamPos	-	vNewMax+vHalfSize*0.5;

				float3	fNearHit	=	p+dir*fNearFar.x;
				float3	fFarHit		=	p+dir*fNearFar.y;

				float2	vDot		=	float2(dot(fNearHit,vVoxelNormal.xyz),dot(fFarHit,vVoxelNormal.xyz))-vVoxelNormal.w;
				if(vDot.x<=0||vDot.y <=0){

					retColor=voxelColor[uiOffset[uiDepth]+childidx];

					iter=8;
					uiDepth=0;
				}else{
					iter++;
				}
#else
				retColor=voxelColor[uiOffset[uiDepth]+childidx];
				iter=8;
				uiDepth=0;
#endif
				continue;			
			}else{
				uiStack&=0xfffffff8;
				uiStack|=iter;
				uiEndStack&=0xfffffff8;
				uiEndStack|=iterSize;
				uiDepth++;
				
				
				uiOffset[uiDepth]	=	uiChild;
				vHalfSize	/=	2.0f;
				vMin		=	vNewMin;


				uint2	uiNearFarIndex		=	PositionToIndex(vCamPos,dir,fNearFar,vMin,vHalfSize);;
				uiStack<<=3;
				uiEndStack<<=3;
				iter=(uiOrderToIndex>>(uiNearFarIndex.x*3))&0x7;
				iterSize=((uiOrderToIndex>>(uiNearFarIndex.y*3))&0x7);


				continue;
			}
		}
		if(uiDepth<=0){
			break;
		}else{
			uiDepth--;
			vHalfSize*=2.0f;
			uiStack>>=3;
			uiEndStack>>=3;
			int iIDX	=	(uiStack&0x7);
			uint childidx	=	(uiOrder>>(iIDX*3))&0x7;
			vMin	-=	float3(childidx>>2,1&(childidx>>1),1&childidx)*vHalfSize;
			iter=iIDX+1;
			iterSize=uiEndStack&0x7;
		}
	}
	if(retColor==0){
		discard;
	}
	return U32_F32(retColor);
//	return float4(1,0,0,0);
};

