#include "common.h"
struct SFragment {
    uint uColor;
    float fDepth;
};

struct SFragmentLink {
    SFragment fragment;
    uint uNext;
};

// Fragment And Link Buffer
RWStructuredBuffer<SFragmentLink>	FLBuffer			: register( u0 );

// Start Offset Buffer
RWByteAddressBuffer					StartOffsetBuffer	: register( u1 );

Texture2D		DiffuseTex		:	register(t0);

SamplerState	SS_WrapLine2	:	register(s0);


struct VS_OUTPUT
{
    float4 Position     : SV_POSITION; // vertex position
	float3 Normal       : TEXCOORD0;
    float2 TextureUV    : TEXCOORD1;   // vertex texture coords 
	float3 Tangent		: TEXCOORD2;
	float4 WPos			: TEXCOORD3;
	float4 ProjPos		: TEXCOORD4;
};
[earlydepthstencil]
void main( VS_OUTPUT input )
{
    uint x = input.Position.x;
    uint y = input.Position.y;

	float4 color	=		DiffuseTex.Sample(SS_WrapLine2,input.TextureUV).xyzz;
	if(color.a<=0.001f){
		return;
	}


    // Create fragment data.
    uint4 ucolor = saturate( color ) * 255;
    SFragmentLink element;
    element.fragment.uColor = (ucolor.x) | (ucolor.y << 8) | (ucolor.z << 16) | (ucolor.a << 24);
    element.fragment.fDepth = input.Position.z;


    // Increment and get current pixel count.
    uint uPixelCount = FLBuffer.IncrementCounter();

	uint g_nFrameWidth	=	1.0f/vVP_NearFar.x;


    // Read and update Start Offset Buffer.
    uint uIndex = y * g_nFrameWidth + x;
    uint uStartOffsetAddress = 4 * uIndex;
    uint uOldStartOffset;
    StartOffsetBuffer.InterlockedExchange( 
        uStartOffsetAddress, uPixelCount, uOldStartOffset );


    // Store fragment link.
    element.uNext = uOldStartOffset;
    FLBuffer[uPixelCount] = element;
}
